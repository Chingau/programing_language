# 1. 链接脚本简介
链接器一般都提供多种控制整个链接过程的方法，以用来产生用户所需要的文件。一般链接器有如下三种方法：

- 使用命令行来给链接器指定参数，例如 ld 的 -o, -e 参数就属于这类。
- 将链接指令存放在目标文件里面，编译器经常会通过这种方式向链接器传递指令。方法也比较常见，只是我们平时很少关注。比如 VISUAL C++编译器会把链接参数放在PE目标文件的 .drectve 段以用传递参数。
- 使用链接控制脚本，此方法最为灵活、最为强大。

# 2. ld链接脚本示例
无论是输出文件还是输入文件，它们的主要的数据就是文件中的各种段，我们把输入文件中的段称为**输入段**，输出文件中的段称为**输出段**。简单来讲，控制链接过程无非是控制输入段如何变成输出段，比如哪些输入段要合并一个输出段，哪些输入段要丢弃；指定输出段的名字、装载地址、属性等等。下面有一个例子：

```
ENTRY(nomain)
SECTIONS
{
    . = 0x08048000 + SIZEOF_HEADERS;
    tinytext : { *(.text) *(.data) *(.rodata) }
    /DISCARD/ : { *(.comment) }
}
```

这是一个非常简单的链接脚本，第一行的 ENTRY(nomain)指定了程序的入口为 nomain() 函数；后面的 SECTIONS 命令一般是链接脚本的主体，这个命令指定了各种输入段到输出段的变换，SECTIONS 后面紧跟着的一对大括号里面包含了 SECTIONS 变换规则，其中有三条诰句，每条语句一行。第一条是赋值语句，后面两条是段转换规则，它们的含义分别如下：

- . = 0x08048000 + SIZEOF_HEADERS 第一条赋值语句的意思是将当前虚拟地址设置成 0x08048000 + SIZEOF_HEADERS，其中的 SIZEOF_HEADERS 为输出文件的文件头大小。"."表示当前虚拟地址，因为这条语句后面紧跟着输出段 "tinytext"，所以 "tinytext" 段的起始虚拟地址即为 0x08048000 + SIZEOF_HEADERS。
- tinytext : { *(.text) *(.data) *(.rodata) } 第二条是个段转换规则，它的意思即为所有输入文件中的名字为 .text，.data，.rodata 的段依次合并到输出文件的 tinytext 段中。
- /DISCARD/ : { *(.comment) } 第三条规则为：将所有输入文件中的名字为 ".comment" 的段丢弃，不保存到输出文件中。


# 3.ld链接脚本语法简介
ld链接器的链接脚本语法继承于AT&T链接器命令语言的语法，风格有点像C语言，这本身并不复杂。链接脚本由一系列语句组成，语句分两种，一种是**命令语句**，另外一种是**赋值语句**。前面第2节中的链接脚本里面 ENTRY(nomain) 就是命令语句；而 . = 0x08048000 + SIZEOF_HEADERS 则是一条经典的赋值语句。之所以说链接语法像C语言，主要有如下几点相似之处：

- <font color=red>**语句之间使用分号";"作为分割符**</font> 原则上讲语句之间都要以";"作为分割符，但是对于命令语句来说也可以使用换行来结束该语句，对于赋值语句来说必须以";"结束。
- <font color=red>**表达式与运算符**</font> 脚本语言的语句中可以使用C语言类似的表达式和运算操作符，比如 +、-、*、/、+=、-=，*= 等，甚至包括 &、|、>>、<< 等位操作符。
- <font color=red>**注释和字符引用**</font> 使用/**/作为注释。脚本文件中使用到的文件名、格式名或段名等凡是包含";"或其它的分隔符的，都要使用双引号将该名字合称引用起来，如果文件名包含引号，则很不幸，无法处理。

命令语句一般的格式是由一个关键字和紧跟其后的参数所组成。比如第2节示例的脚本就是由两个命令语句组成：一个ENTRY命令语句和一个SECTIONS语句，"ENTRY"和"SECTION"为这两个语句的关键字。其中SECTIONS语句比较复杂，它又包含了一个赋值语句及一些SECTIONS语句所特有的映射规则。其实除了SECTIONS命令语句之外，其它命令语句都比较简单，毕竟SECTIONS负责指定链接过程的段转换过程，这也是链接的最核心和最复杂的部分。我们先来看看一些常见的命令语句，如下表：

|命令语句|说明
|:--|:--|
ENTRY(symbol)|指定符号symbol的值为入口地址。入口地址即进程执行的第一条用户空间的指令在进程地址空间的地址，它就是使用readelf -h 输出的文件头中的 Entry point address 的值。ld有多种方法可以设置进程入口地址，它们之间的优先级按以下顺序排列(编号越靠前，优先级越高)：1.ld命令行的-e选项 2.链接脚本的ENTRY(symbol)命令 3.如果定义了_start符号，使用_start符号值 4.如果存在.text段，使用.text段的第一字节地址 5.使用值0
STARTUP(finename)|将文件filename作为链接过程中的第一个输入文件
SEARCH_DIR(path)|将路径path加入到ld链接器的库查找目录。ld会根据指定的目录去查找相应的库，跟"-Lpath"命令有着相同的作用
INPUT(file,file,...)、INPUT(file file ...)|将指定文件作为链接过程中的输入文件
INCLUDE filename|将指定文件包含进本链接脚本。类似于C语言中的#include预处理
PROVIDE(symbol)|在链接脚本中定义某个符号。该符号可以在程序中被引用，其实前文提到的特殊符号都是由系统默认的链接脚本通过PROVIDE命令定义在脚本中的

这里只是大楖提及以下几个常用命令语句格式，更多的命令语句的意义及它们的格式请参照ld的使用手册。除了这些简单的命令语句之外，剩下最重要、也是最复杂的就是SECTIONS命令了。SECTIONS命令语句最基本格式为：

```
SECTIONS
{
    ...
    secname : { contents }
    ...
}
```

secname表示输出段的段名，secname后面必须有一个空格符，这样使得输出段名不会有歧义，后面紧跟着冒号和一对大括号。大括号里面的 contents 描述了一套规则和条件，它表示符合这种条件的输入段将合并到这个输出段中。输出段名的命令方法必须符合输出文件格式的要求，比如，如果使用ld生产一个a.out格式的文件，那么输出段名就不可以使用除".text",".data",".bss"之外的任何名字，因为a.out格式规定段名只允许这三个名字。

有一个特殊的段名叫"/DISCARD/"，如果使用这个名字作为输出段名，那么所有符合后面contents所规定的条件的段都将被丢弃，不输出到输出文件中。

接着，我们最应该关心的是contents这个规则。contents中可以包含若干个条件，每个条件之间以空格隔开，如果输入段符合这些条件中的任意一个即表示这个输入段符合contents规则。条件的写法如下：

```
filename(sections)
```

filename表示输入文件名，sections表示输入段名。以下是几个示例：

- **file1.o(.data)** 表示输入文件中名为file1.o的文件中名叫.data的段符合条件。
- **file1.o(.data .rodata) 或 file1.o(.data, .rodata)** 表示输入文件中名为file1.o的文件中的名叫.data或.rodata的段符合条件。
- **file1.o** 如果直接指定文件名而省略后面的小括号和段名，则表示file1.o的所有段都符合条件。
- ***(.data)** 所有输入文件中的名字为.data的文件符合条件。\*是通配符，类似于正则表达式中的\*，我们还可以使用正则表达式中的 ?、[] 等规则。
- **[a-z]\*(.text\*[A-Z])** 这个条件比较复杂，它表示所有输入文件中以小写字母a到z开头的文件中甩有段名以.text开头，并且以大写字母A到Z结尾的段都符合条件。



